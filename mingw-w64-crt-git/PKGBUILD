# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=crt
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=12.0.0.r682.g0961ee965
pkgrel=1
pkgdesc='MinGW-w64 CRT for Windows (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://www.mingw-w64.org/'
msys2_repository_url="https://sourceforge.net/p/mingw-w64/mingw-w64/ci/master/tree/mingw-w64-crt/"
license=('spdx:ZPL-2.1')
groups=("${MINGW_PACKAGE_PREFIX}-toolchain")
depends=("${MINGW_PACKAGE_PREFIX}-headers-git")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-binutils"
             "git")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
options=('!strip' '!buildflags' '!emptydirs')
source=("mingw-w64"::"git+https://git.code.sf.net/p/mingw-w64/mingw-w64"
        0001-crt-delayimp-Make-an-IAT-entry-writeable-before-modi.patch
        9003-crt-Implement-standard-conforming-termination-suppor.patch
        9004-crt-Copy-clock-and-nanosleep-from-winpthreads.patch)
sha256sums=('SKIP'
            'fcef8b83090c57ff897f471aeb0cd5a71d6385ff569eba8bbef0c424c21acda4'
            '36046863089ab8ae779f9f73819d30f5f98c61b79c7ec1e43e7ad491fdb044c2'
            '5e16165d1ddc215be26f7b4a145a99555540426fe46cc1f1ba57aca57fb189fc')

pkgver() {
  cd "${srcdir}/mingw-w64"
  git describe --long HEAD | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//g'
}

prepare() {
  cd "${srcdir}/mingw-w64"
  git reset --hard
  git clean -fdx

  patch -p1 -i "${srcdir}/0001-crt-delayimp-Make-an-IAT-entry-writeable-before-modi.patch"

  git apply "${srcdir}/9003-crt-Implement-standard-conforming-termination-suppor.patch"
  git apply "${srcdir}/9004-crt-Copy-clock-and-nanosleep-from-winpthreads.patch"
  (cd mingw-w64-crt && autoreconf)
}

build() {
  cd "${srcdir}/mingw-w64"
  declare -a _extra_config
  case "$CARCH" in
    i686)
      _extra_config=("--disable-lib64" "--enable-lib32")
    ;;
    x86_64)
      _extra_config=("--disable-lib32" "--enable-lib64")
    ;;
    armv7)
      _extra_config=("--disable-lib32" "--disable-lib64" "--enable-libarm32")
    ;;
    aarch64)
      _extra_config=("--disable-lib32" "--disable-lib64" "--disable-libarm32" "--enable-libarm64")
    ;;
  esac

  # only clang+lld support this at the moment
  if [[ $MINGW_PACKAGE_PREFIX == *-clang-* ]]; then
    _extra_config+=("--enable-cfguard")
  fi

  if [[ $MINGW_PACKAGE_PREFIX == *-clang-* ]] || [[ $MINGW_PACKAGE_PREFIX == *-ucrt-* ]]; then
    _extra_config+=("--with-default-msvcrt=ucrt")
  else
    _extra_config+=("--with-default-msvcrt=msvcrt")
  fi

  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  "${srcdir}"/mingw-w64/mingw-w64-crt/configure \
    --prefix=${MINGW_PREFIX} \
    --disable-dependency-tracking \
    "${_extra_config[@]}"

  make
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  make DESTDIR="${pkgdir}" install-strip

  # Create empty dummy archives, to avoid failing when the compiler driver
  # adds -lssp -lssh_nonshared when linking.
  ar rcs "${pkgdir}"${MINGW_PREFIX}/lib/libssp.a
  ar rcs "${pkgdir}"${MINGW_PREFIX}/lib/libssp_nonshared.a

  install -Dm644 "${srcdir}"/mingw-w64/COPYING "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
  install -Dm644 "${srcdir}"/mingw-w64/COPYING.MinGW-w64/COPYING.MinGW-w64.txt "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.MinGW-w64.txt
  install -Dm644 "${srcdir}"/mingw-w64/COPYING.MinGW-w64-runtime/COPYING.MinGW-w64-runtime.txt "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.MinGW-w64-runtime.txt
}
