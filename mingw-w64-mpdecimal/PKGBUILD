# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=mpdecimal
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.0.0
pkgrel=1
pkgdesc="Package for correctly-rounded arbitrary precision decimal floating point arithmetic (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://www.bytereef.org/mpdecimal/index.html'
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
checkdepends=("wget")
source=("http://www.bytereef.org/software/${_realname}/releases/${_realname}-$pkgver.tar.gz")
sha256sums=('942445c3245b22730fd41a67a7c5c231d11cb1b9936b9c0f76334fb7d0b4468c')

prepare() {
  cd $srcdir/${_realname}-${pkgver}

  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  cp -R "$srcdir/${_realname}-${pkgver}" "${srcdir}"/build-${MSYSTEM}
  cd "${srcdir}"/build-${MSYSTEM}

  if [[ "${CARCH}" == "x86_64" || "${CARCH}" == "i686" ]]; then
    export CFLAGS+=' -masm=att'
    export CXXFLAGS+=' -masm=att'
  fi

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}

  make
}

check() {
  cd "${srcdir}"/build-${MSYSTEM}
  make check
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  make install DESTDIR="${pkgdir}"
}
