# Maintainer: LIU Hao <lh_mouse@126.com>

_realname=kcachegrind
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-kcachegrind")
pkgver=25.03.80
pkgrel=1
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url="https://invent.kde.org/sdk/kcachegrind"
license=('spdx:GPL-2.0-only')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-icoutils"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-qt6-base"
             "${MINGW_PACKAGE_PREFIX}-qt6-tools"
             "${MINGW_PACKAGE_PREFIX}-qt6-imageformats"
             "${MINGW_PACKAGE_PREFIX}-extra-cmake-modules"  # install `*-kf6` instead
             "${MINGW_PACKAGE_PREFIX}-karchive"             #
             "${MINGW_PACKAGE_PREFIX}-kdoctools"            #
             "${MINGW_PACKAGE_PREFIX}-kcoreaddons"          #
             "${MINGW_PACKAGE_PREFIX}-kwidgetsaddons"       #
             "${MINGW_PACKAGE_PREFIX}-kxmlgui"              #
             "${MINGW_PACKAGE_PREFIX}-ki18n"                #
             "${MINGW_PACKAGE_PREFIX}-kconfig"              #
             "${MINGW_PACKAGE_PREFIX}-kio"                  #
             "${MINGW_PACKAGE_PREFIX}-kdbusaddons"          #
             'git')
source=("${_realname}"::"git+https://invent.kde.org/sdk/kcachegrind.git#tag=v${pkgver}"
        "0001-zap-bad-constexpr.patch")
sha256sums=('7487ec45a2e547b02a57eabb0028261936b073bea1ba12ec0a35ecceb54d1a91'
            '0ba354fdb797a498c457767f98e1babbd4e83114808b7108754c417ffd9716ea')

prepare() {
  cd "${_realname}"

  patch -Np1 -i "${srcdir}/0001-zap-bad-constexpr.patch"
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DBUILD_{SHARED,STATIC}_LIBS=ON \
      -S "${_realname}" \
      -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

package() {
  pkgdesc+="KCachegrind (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-karchive"        # install `*-kf6` instead
           "${MINGW_PACKAGE_PREFIX}-kxmlgui"         #
           "${MINGW_PACKAGE_PREFIX}-kconfigwidgets"  #
           "${MINGW_PACKAGE_PREFIX}-kdbusaddons"     #
           "${MINGW_PACKAGE_PREFIX}-gcc-libs"
           "${MINGW_PACKAGE_PREFIX}-qt6-base")
  optdepends=("${MINGW_PACKAGE_PREFIX}-graphviz: For call graphs"
              "${MINGW_PACKAGE_PREFIX}-binutils: For disassembly")

  DESTDIR="${pkgdir}/tmp" cmake --install "build-${MSYSTEM}"

  mkdir -p "${pkgdir}${MINGW_PREFIX}/bin"
  mv "${pkgdir}/tmp${MINGW_PREFIX}/bin/kcachegrind.exe" -t "${pkgdir}${MINGW_PREFIX}/bin"

  rm -rf "${pkgdir}/tmp"

  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/licenses/kcachegrind"
  cp -r "${srcdir}/${_realname}/LICENSES"/* -t "${pkgdir}${MINGW_PREFIX}/share/licenses/kcachegrind"
  cp -r "${srcdir}/${_realname}/README" -t "${pkgdir}${MINGW_PREFIX}/share/licenses/kcachegrind"
}
